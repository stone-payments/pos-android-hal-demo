// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {

    ext {
        versions = [
                android_compile_sdk  : 31,
                android_min_sdk      : 22,
                android_target_sdk   : 31,
                androidx_test_version: "1.3.0",
                build_tools          : "29.0.2",
                build_gradle         : "3.5.0",
                kotlin               : "1.6.0",
                kotlinx              : "1.6.0",
                dokka                : "0.9.18",
                junit5               : "5.5.0",
                junit                : "4.12",
                test_runner          : "1.0.2",
                gson                 : "2.8.6",
                halApi               : "2.2.1-RC2-PR_177-11feeee"
        ]
        localProp = new Properties()
        fileName = 'local.properties'
        if (project.rootProject.file(fileName).exists()) {
            localProp.load(new FileInputStream(rootProject.file(fileName)))
        }
        packageCloudReadToken = System.env.PACKAGECLOUD_TOKEN != null ? System.env.PACKAGECLOUD_READ_TOKEN  : localProp["packageCloudReadToken"] ?: ""
        packageCloudReadTokenInternal = System.env.PACKAGECLOUD_TOKEN_INTERNAL != null ? System.env.PACKAGECLOUD_READ_TOKEN_INTERANL  : localProp["packageCloudReadTokenInternal"] ?: ""
    }

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:${versions.build_gradle}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "org.jetbrains.dokka:dokka-android-gradle-plugin:${versions.dokka}"
    }

}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url "https://packagecloud.io/stone/pos-android-hal/maven2" }
        maven { url "https://packagecloud.io/stone/pos-android-hal-internal/maven2" }
        maven { url "https://packagecloud.io/stone/sdk-android/maven2" }
        maven { url "https://packagecloud.io/priv/${packageCloudReadToken}/stone/pos-android/maven2" }
        maven { url "https://packagecloud.io/priv/${packageCloudReadTokenInternal}/stone/pos-android-internal/maven2" }
    }
    group = "br.com.stone.posandroid"

    configurations.all {
        resolutionStrategy.force 'org.objenesis:objenesis:2.6'
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
