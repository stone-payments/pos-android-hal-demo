def keystorePropertiesFile = rootProject.file("positivo/positivo-keystore.properties")
def keystoreProperties = new Properties()

if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {

    signingConfigs {
        positivo {
            storeFile file("../positivo/${keystoreProperties.getProperty('storeFile', System.env.POSITIVO_KEYSTORE_FILE)}")
            keyAlias keystoreProperties.getProperty('keyAlias', System.env.POSITIVO_KEYSTORE_ALIAS)
            keyPassword keystoreProperties.getProperty('keyPassword', System.env.POSITIVO_KEYSTORE_KEY_PASSWORD)
            storePassword keystoreProperties.getProperty('storePassword', System.env.POSITIVO_KEYSTORE_PASSWORD)
        }
    }


    productFlavors {
        positivo {
            dimension "manufacturer"
            signingConfig signingConfigs.positivo
            versionNameSuffix "-positivo"
        }
    }


    variantFilter { variant ->

        def manufacturer = variant.getFlavors().get(0).name
        if (manufacturer == 'positivo' && variant.buildType.name == "gertec") {
            setIgnore(true)
        }
    }
}